message(STATUS "Found OusterSDK: ${CMAKE_CURRENT_LIST_FILE}")

include(CMakeFindDependencyMacro)

# ouster_client dependencies
find_dependency(Eigen3)
find_dependency(jsoncpp)
find_dependency(CURL)

# viz dependencies
if(@BUILD_VIZ@)
  set(OpenGL_GL_PREFERENCE GLVND)
  find_dependency(OpenGL)
  find_dependency(Threads)
  find_dependency(glfw3)

  if(@OUSTER_VIZ_USE_GLAD@)
    find_dependency(glad)
  else()
    find_dependency(GLEW)
  endif()
endif()

if(@BUILD_PCAP@)
  # libpcap dep doesn't use cmake targets, lib and include paths are fixed at
  # build time. Seems like this works for deb packages. This pcapplusplus
  # dependency will only work under conan, so the deb package is currently
  # broken. Recording the dependency at least produces a clear error message.
  #
  # TODO: update this cmake config so it at least works when pcapplusplus has
  # been installed on the system?
  find_dependency(pcapplusplus)
endif()

include("${CMAKE_CURRENT_LIST_DIR}/OusterSDKTargets.cmake")
